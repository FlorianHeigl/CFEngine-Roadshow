bundle agent update {

files:
   "$(sys.workdir)/inputs"
           comment => "Copy policy updates from master source on policy server if a new validation was acquired",
            handle => "update_files_inputs_dir",
         copy_from => u_secure_cp("${g.master_location}","${sys.policy_hub}"),
      depth_search => u_recurse("inf"),
      file_select  => u_inputfiles,
            action => u_immediate,
            create => "true",
           classes => u_if_repaired("update_report");

   "$(sys.workdir)/dynamic-inputs/application"
           comment => "Copy application policy updates from master source on policy server if a new validation was acquired",
            handle => "update_files_inputs_application",
         copy_from => u_secure_cp("${g.master_dynamic}/application/${g.application}","${sys.policy_hub}"),
      depth_search => u_recurse("inf"),
      file_select  => u_inputdomain,
            action => u_immediate,
           classes => u_if_repaired("update_report");

   "$(sys.workdir)/dynamic-inputs/function"
           comment => "Copy function policy updates from master source on policy server if a new validation was acquired",
            handle => "update_files_inputs_function",
         copy_from => u_secure_cp("${g.master_dynamic}/function/${g.function}","${sys.policy_hub}"),
      depth_search => u_recurse("inf"),
      file_select  => u_inputdomain,
            action => u_immediate,
           classes => u_if_repaired("update_report");

   "$(sys.workdir)/dynamic-inputs/platform"
           comment => "Copy platform policy updates from master source on policy server if a new validation was acquired",
            handle => "update_files_inputs_platform",
         copy_from => u_secure_cp("${g.master_dynamic}/platform/${g.os}","${sys.policy_hub}"),
      depth_search => u_recurse("inf"),
      file_select  => u_inputfiles,
            action => u_immediate,
           classes => u_if_repaired("update_report");

reports:
 "upd: platform: ${g.master_dynamic}/platform/${g.os}/*.cf";
}

#########################################################
body copy_from u_secure_cp(from,server)
{
 source      => "$(from)";
 servers     => { "$(sys.policy_hub)" };
 trustkey    => "false";
 encrypt     => "true";
 purge       => "true";

#!am_policy_hub::

 servers => { "$(sys.policy_hub)" };
}
#########################################################

body action u_immediate
{
 ifelapsed => "0";
}

#########################################################

body depth_search u_recurse(d)
{
 depth => "$(d)";
 exclude_dirs => { "\.svn", "\.git" };
}

body file_select u_inputfiles
{
 leaf_name => { ".*.cf",".*.dat",".*.txt" };
 file_result => "leaf_name";
}

body file_select u_inputdomain
{
 leaf_name => { ".*.cf",".*.dat",".*.txt" };
 file_result => "leaf_name";
}

body file_select u_inputhost
{
 leaf_name => { ".*.cf",".*.dat",".*.txt" };
 file_result => "leaf_name";
}
body classes u_if_repaired(x)
{
 promise_repaired => { "$(x)" };
}

