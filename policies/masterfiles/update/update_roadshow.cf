#########################################################
#
# update_roadshow
# - Cfengine roadshow policy update (masterfiles -> inputs)
#
#########################################################

bundle agent cfe_roadshow_update_policy
{
  classes:
      "parsed_all" expression => regextract("^(...)(...)(..).*", $(sys.uqhost), "part");

  vars:
      "application"          string => "${part[1]}";
      "function"             string => "${part[2]}";
      "master_dynamic"       string => "$(sys.workdir)/dynamic_policies";

  files:
      # note that validated_updates_ready is defined by
      # cfe_internal_update_policy as a global class so we can use it
    am_policy_hub|validated_updates_ready::

      "$(sys.workdir)/dynamic-inputs/application"
      comment => "Copy application policy updates from master source on policy server if a new validation was acquired",
      handle => "update_files_inputs_application",
      copy_from => u_rcp("${master_dynamic}/application/${application}","${sys.policy_hub}"),
      depth_search => u_recurse("inf"),
      file_select  => u_input_files,
      action => u_immediate,
      classes => u_if_repaired("update_report");

      "$(sys.workdir)/dynamic-inputs/function"
      comment => "Copy function policy updates from master source on policy server if a new validation was acquired",
      handle => "update_files_inputs_function",
      copy_from => u_rcp("${master_dynamic}/function/${function}","${sys.policy_hub}"),
      depth_search => u_recurse("inf"),
      file_select  => u_input_files,
      action => u_immediate,
      classes => u_if_repaired("update_report");

      "$(sys.workdir)/dynamic-inputs/platform"
      comment => "Copy platform policy updates from master source on policy server if a new validation was acquired",
      handle => "update_files_inputs_platform",
      copy_from => u_rcp("${master_dynamic}/platform/${sys.flavor}","${sys.policy_hub}"),
      depth_search => u_recurse("inf"),
      file_select  => u_input_files,
      action => u_immediate,
      classes => u_if_repaired("update_report");

      "$(sys.workdir)/dynamic-inputs/platform_application/${sys.flavor}_${application}.cf"
      comment => "Copy platform_applicatiion policy updates from master source on policy server if a new validation was acquired",
      handle => "update_files_inputs_platform_application",
      copy_from => u_rcp("${master_dynamic}/platform_application/${sys.flavor}/${sys.flavor}_${application}.cf","${sys.policy_hub}"),
      depth_search => u_recurse("inf"),
      file_select  => u_input_files,
      action => u_immediate,
      classes => u_if_repaired("update_report");
}
